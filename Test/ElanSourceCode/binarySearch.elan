main
    var li = {"apple","apricot","banana","lemon","lime","melon","orange","pear","plum","strawberry"}
    call printLine(binarySearch(li, "lemon"))
    call printLine(binarySearch(li, "apple"))
    call printLine(binarySearch(li, "strawberry"))
    call printLine(binarySearch(li, "blueberry"))
end main

function binarySearch(li List<String>, item String) ->  Bool 
  var result = false
  if li.length() is 0 then  
    set result to false
  else if li.length() is 1 then
    set result to li[0] is item
  else 
    var mid = li.length() div 2
    var value = li[mid]
    if item is value then
        set result to true
    else if item.isBefore(value) then
        set result to binarySearch(li[0..mid], item) 
    else 
        set result to binarySearch(li[mid+1..], item)
    end if
  end if
  return result
end function