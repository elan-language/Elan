main
    print welcome
    var k = readKey()
    var newGame = true
    while newGame
        call playGame()
        print "Do you want to play again (y/n)?"
        var answer = ' '
        repeat
            set answer to readKey()
        until answer is 'y' or answer is 'n'
        if answer is 'n' then
            set newGame to false
        end if
    end while
end main

constant bodyColour = Colour.green
constant appleColour = Colour.red
constant directionByKey = { 'w': Direction.up, 's' : Direction.down, 'a': Direction.left, 'd': Direction.right}
constant welcome = "Welcome to the Snake game. 

Use the w,a,s, and d keys to control the direction of the snake. Letting the snake get to any edge will lose you the game, as will letting the snake's head pass over its body. Eating an apple will
cause the snake to grow by 

If you want to re-size the window, please do so now, before starting the game.

Click on this window to get 'focus' (and see a flashing cursor). Then press any key to start.."

procedure playGame()
    var charMap = new CharMap()
    call charMap.fillBackground()
    var currentDirection = Direction.right
    var game = new Game((charMap.width/2).asInt(), charMap.height, currentDirection)
    var gameOn = true
    call game.setNewApplePosition()
    while gameOn
        call draw(charMap, game.head(), bodyColour)
        call draw(charMap, game.apple, appleColour)
        var priorTail = game.tail()
        call pause(200)
        var pressed = keyHasBeenPressed()
        if pressed then
            var k = readKey()
            set currentDirection to directionByKey[k]
        end if
        call game.clockTick(currentDirection, gameOn)
        if game.tail() is not priorTail then
            call draw(charMap, priorTail, charMap.backgroundColour)
        end if
    end while
    call clearKeyBuffer()
    call charMap.setCursor(0, 0)
    print "Game Over! Score: {game.getScore()}"
end procedure

procedure draw(cm CharMap, sq Square, colour Colour)
    var col = sq.x * 2
    var row = sq.y
    call cm.putBlockWithColour(col, row, colour)
    call cm.putBlockWithColour(col + 1, row, colour) # because block is 2 chars wide
end procedure

class Game    
    constructor (width Int, height Int, startingDirection Direction)
        set self.width to width
        set self.height to height
        set snake to new Snake(width div 2, height div 2, startingDirection) 
        call setNewApplePosition()
    end constructor

    private property width Int
    private property height Int
    private property snake Snake
    property apple Square

    procedure clockTick(d Direction, out continue Bool)
        call snake.advanceHeadOneSquare(d)
        if snake.head is apple then
            call setNewApplePosition()
        else
            call snake.advanceTailOneSquare()
        end if
        set continue to not hasHitEdge() and not snake.hasHitItself()
    end procedure

    function hasHitEdge() as Bool
        var x = snake.head.x
        var y = snake.head.y
        return x < 0 or y < 0 or x is width or y is height
    end function

    procedure setNewApplePosition()
        var sq = new Square(0,0)
        var collision = false
        repeat
            var ranW = random(width-1)
            var ranH = random(height-1)
            set sq to new Square(ranW, ranH) 
        until not snake.bodyCovers(sq)
        set apple to sq
    end procedure

    function head() as Square -> snake.hea
    function tail() as Square -> snake.tail()
    function getScore() as Int -> snake.length() - 2
    function asString() as String -> "Game"
end class

class Snake
    constructor(x Int, y Int, startingDirection Direction)
        var tail = new Square(x, y)
        set body to {tail}
        set head to tail.getAdjacentSquare(startingDirection)
    end constructor

    private property body List<of Square>
    property head Square

    function bodyCovers(sq Square) as Bool
      var result = false
      foreach seg in body
        if (seg is sq) then
          set result to true
        end if
      end foreach
      return result
    end function

    procedure advanceHeadOneSquare(d Direction)
        set body to body + head
        set head to head.getAdjacentSquare(d)
    end procedure

    procedure advanceTailOneSquare()
        set body to body[1..]
    end procedure

    function length() as Int -> body.length()
    function tail() as Square  -> body[0]
    function hasHitItself() as Bool -> bodyCovers(head)
    function asString() as String -> "Snake"
end class

class Square
    constructor (x Int, y Int)
        set self.x to x
        set self.y to y
    end constructor

    property x Int
    property y Int

    function getAdjacentSquare(d Direction) as Square
        var newX = x
        var newY = y
        switch d
            case Direction.left
                set newX to newX - 1     
            case Direction.right
                set newX to newX + 1
            case Direction.up
                set newY to newY - 1 
            case Direction.down
                set newY to newY + 1
            default
        end switch
        return new Square(newX, newY)
    end function

    function asString() as String  -> "{x},{y}"
end class